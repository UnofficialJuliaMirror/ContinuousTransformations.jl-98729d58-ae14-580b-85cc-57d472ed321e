module ContinuousTransformations

using StatsFuns
using ValidatedNumerics
using Parameters
import Base: inv
using ArgCheck

export
    # general
    LOGJAC,
    JAC,
    UnivariateTransformation,
    domain,
    integral_substitution,
    # univariate transformations
    Logistic,
    Logit,
    Exp,
    Log,
    OddsRatio,
    InvOddsRatio,
    Affine,
    Power

######################################################################
# general interface
######################################################################

immutable LogJac end

const LOGJAC = LogJac()

immutable Jac end

const JAC = Jac()

ùïÄ(T) = zero(T)..one(T)

inùïÄ(x) = zero(x) ‚â§ x ‚â§ one(x)

‚Ñù(T) = T(-Inf)..T(Inf)

‚Ñù‚Å∫(T) = zero(T)..T(Inf)

in‚Ñù‚Å∫(x) = zero(x) ‚â§ x

abstract UnivariateTransformation

"""
Return the domain of the transformation as an interval, with the given
type (defaults to Float64).
"""
domain(t::UnivariateTransformation) = domain(Float64, t)

"""
Transform an integrand and a domain for integration using `t` as the
substitution. Return the transformed function and the domain.

Example:

```julia
f, D = integral_substitution(InvOddsRatio(), x->exp(-x^2), 0..Inf)
```

will return values such that
``
\int_D f(x) dx = \int_0^\infty exp(-x^2) dx = ‚àöœÄ/2
``
"""
function integral_substitution(t, f, domain)
    t‚Åª = inv(t)
    function(y)
        x, jac = t‚Åª(y, JAC)
        f(x)*jac
    end, t(domain)
end

"""
Apply a monotone transformation to an interval by endpoints, using
correct rounding (depending, of course, on `f` respecting rounding
mode).
"""
@inline function map_interval_monotone{T}(f, x::Interval{T}, increasing::Bool = true)
    f_rounded(x, mode) = setrounding(()->f(x), T, mode)
    if increasing
        Interval(f_rounded(x.lo, RoundDown), f_rounded(x.hi, RoundUp))
    else
        Interval(f_rounded(x.hi, RoundDown), f_rounded(x.lo, RoundUp))
    end
end

"""
Given a monotone increasing function `f` that operates on scalars,
define a mode for intervals.
"""
macro lift_monotone_increasing(T)
    quote
        (f::$T)(x::Interval) = map_interval_monotone(f, x)
    end
end

######################################################################
# logistic
######################################################################

"Transform ‚Ñù to (0,1) using the logistic function."
immutable Logistic <: UnivariateTransformation end

domain{T <: Real}(::Type{T}, ::Logistic) = ‚Ñù(T)

(::Logistic)(x) = logistic(x)

@lift_monotone_increasing Logistic

(f::Logistic)(x, ::Jac) = (‚Ñì = f(x); (‚Ñì, exp(-x)*‚Ñì^2))

(f::Logistic)(x, ::LogJac) = f(x), -x-2*log1pexp(-x)

inv(::Logistic) = Logit()

######################################################################
# logit
######################################################################

"""
Transfrom (0,1) to ‚Ñù using the logit function.
"""
immutable Logit <: UnivariateTransformation end

domain{T <: Real}(::Type{T}, ::Logit) = ùïÄ(T)

function (::Logit)(x)
    @argcheck inùïÄ(x) DomainError()
    logit(x)
end

@lift_monotone_increasing Logit

(f::Logit)(x, ::Jac) = f(x), 1/(x*(1-x))

(f::Logit)(x, ::LogJac) = f(x), -(log(x)+(log(1-x)))

inv(::Logit) = Logistic()

######################################################################
# odds ratio and its inverse
######################################################################

"""
Maps ``(0,1)`` to ``(0, ‚àû)`` using ``y = x/(1-x)``.
"""
immutable OddsRatio <: UnivariateTransformation end

domain{T <: Real}(::Type{T}, ::OddsRatio) = ùïÄ(T)

function (::OddsRatio)(x)
    @argcheck inùïÄ(x) DomainError()
    x/(one(x)-x)
end

@lift_monotone_increasing OddsRatio

(f::OddsRatio)(x, ::Jac) = f(x), one(x)/((one(x)-x)^2)

(f::OddsRatio)(x, ::LogJac) = f(x), -2*log(1-x)

inv(::OddsRatio) = InvOddsRatio()

"""
Maps ``(0,‚àû)`` to ``(0, 1)`` using ``y = x/(1+x)``.
"""
immutable InvOddsRatio <: UnivariateTransformation end

domain{T <: Real}(::Type{T}, ::InvOddsRatio) = ‚Ñù‚Å∫(T)

function (::InvOddsRatio)(x)
    @argcheck in‚Ñù‚Å∫(x) DomainError()
    x == Inf ? one(x) : x/(1+x)
end

@lift_monotone_increasing InvOddsRatio

(f::InvOddsRatio)(x, ::Jac) = f(x), (1+x)^(-2)

(f::InvOddsRatio)(x, ::LogJac) = f(x), -2*log1p(x)

inv(::InvOddsRatio) = OddsRatio()

######################################################################
# exponential and log
######################################################################

"Transform ‚Ñù to the interval (0,‚àû), using the exponential function."
immutable Exp <: UnivariateTransformation end

domain{T <: Real}(::Type{T}, ::Exp) = ‚Ñù(T)

(::Exp)(x) = exp(x)

(::Exp)(x, ::Jac) = (œµ = exp(x); (œµ,œµ))

(::Exp)(x, ::LogJac) = exp(x), x

inv(::Exp) = Log()

"""
Transform (0,‚àû) to ‚Ñù  using the logarithm function.
"""
immutable Log <: UnivariateTransformation end

domain{T <: Real}(::Type{T}, ::Log) = ‚Ñù‚Å∫(T)

(::Log)(x) = log(x)

(::Log)(x, ::Jac) = log(x), 1/x

(::Log)(x, ::LogJac) = (‚Ñì=log(x); (‚Ñì, -‚Ñì))

inv(::Log) = Exp()

######################################################################
# affine transformation
######################################################################

"""
Transform ‚Ñù to itself using ``y = Œ±‚ãÖx + Œ≤``.
"""
immutable Affine{T <: Real} <: UnivariateTransformation
    Œ±::T
    Œ≤::T
    function Affine(Œ±, Œ≤)
        @argcheck Œ± ‚â† zero(T) DomainError()
        new(Œ±, Œ≤)
    end
end

Affine{T}(Œ±::T, Œ≤::T) = Affine{T}(Œ±, Œ≤)

Affine(Œ±, Œ≤) = Affine(promote(Œ±, Œ≤)...)

domain{T <: Real}(::Type{T}, ::Affine) = ‚Ñù(T)

(a::Affine)(x) = fma(x, a.Œ±, a.Œ≤)

(a::Affine)(x::Interval) = map_interval_monotone(a, x, a.Œ± > 0)

(a::Affine)(x, ::Jac) = a(x), abs(a.Œ±)

(a::Affine)(x, ::LogJac) = a(x), log(abs(a.Œ±))

function inv{T}(a::Affine{T})
    @unpack Œ±, Œ≤ = a
    Affine(one(T)/Œ±, -Œ≤/Œ±)
end

"""
Return an Affine map that maps the first interval to the second.
"""
function Affine(i1::Interval, i2::Interval)
    @argcheck isfinite(i1) && isfinite(i2) "infinite interval(s)"
    d1 = diam(i1)
    d2 = diam(i2)
    @argcheck d1 > 0 && d2 > 0 "empty interval(s)"
    Œ± = d2 / d1
    Œ≤ = i2.lo - i1.li * Œ±
    Affine(Œ±, Œ≤)
end

######################################################################
# power transformation
######################################################################

immutable Power{T <: Real} <: UnivariateTransformation
    Œ≥::T
    function Power(Œ≥)
        @assert Œ≥ > zero(Œ≥)
        new(Œ≥)
    end
end

Power{T}(Œ≥::T) = Power{T}(Œ≥)

domain{T}(::Type{T}, ::Power) = ‚Ñù‚Å∫(T)

function (p::Power)(x)
    @argcheck in‚Ñù‚Å∫(x) DomainError()
    x^p.Œ≥
end

@lift_monotone_increasing Power

(p::Power)(x, ::Jac) = p(x), p.Œ≥*x^(p.Œ≥-1)

(p::Power)(x, ::LogJac) = p(x), log(p.Œ≥)+(p.Œ≥-1)*log(x)

inv(p::Power) = Power(1/p.Œ≥)

end # module
